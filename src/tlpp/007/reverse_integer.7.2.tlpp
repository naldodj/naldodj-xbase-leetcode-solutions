/*
    7. Reverse Integer

    Given a signed 32-bit integer x, return x with its digits reversed. if reversing x causes the value to go outside the signed 32-bit integer range [-2^31, 2^31 - 1], then return(0).

    Assume the environment does not allow you to store 64-bit integers (signed or unsigned).

    Example 1:

    Input: x = 123
    Output: 321

    Example 2:

    Input: x = -123
    Output: -321

    Example 3:

    Input: x = 120
    Output: 21

    Constraints:

    -2^31 <= x <= 2^31 - 1

    Released to Public Domain.
    --------------------------------------------------------------------------------------

*/

namespace dna.tech.leetcode

static __cOut:="" as character

#xcommand ?  [ <vList,...> ] => ( [, __cOut+=__ConOut( <vList> ) ] )

#xtranslate "</br>" => chr(13)+chr(10)

#xtranslate IsNil(<xValue>) => Eval({|xValue|xValue==NIL,<xValue>})
#xtranslate hb_NToC(<n>) => LTrim(Str(<n>))
#xtranslate hb_Default(@<uVar1>,<uVal1>) => <uVar1> := iif( IsNil(<uVar1>), <uVal1>, <uVar1> )

procedure u_MainReverseInteger_7_2()

    local nInput as numeric
    local nOutPut as numeric

    nInput:=123
    ? "Input: ",nInput,"</br>"
    nOutPut:=ReverseNumber(nInput)
    ? "Output: ",hb_NToC(nOutPut),"</br>","</br>"

    nInput*=-1
    ? "Input: ",nInput,"</br>"
    nOutPut:=ReverseNumber(nInput)
    ? "Output: ",hb_NToC(nOutPut),"</br>","</br>"

    nInput:=7463847412
    ? "Input: ",nInput,"</br>"
    nOutPut:=ReverseNumber(nInput)
    ? "Output: ",hb_NToC(nOutPut),"</br>","</br>"

    nInput:=-8463847412
    ? "Input: ",nInput,"</br>"
    nOutPut:=ReverseNumber(nInput)
    ? "Output: ",hb_NToC(nOutPut),"</br>","</br>"

    nInput:=2147483647
    ? "Input: ",nInput,"</br>"
    nOutPut:=ReverseNumber(nInput)
    ? "Output: ",hb_NToC(nOutPut),"</br>","</br>"

    nInput:=-2147483648
    ? "Input: ",nInput,"</br>"
    nOutPut:=ReverseNumber(nInput)
    ? "Output: ",hb_NToC(nOutPut),"</br>","</br>"

    MsgInfo(__cOut,ProcName())

    __cOut:=""

    return

static function ReverseNumber(nNumber as numeric,nMin as numeric,nMax as numeric)

    local nAbs:=Abs(nNumber) as numeric // Trabalha com o valor absoluto
    local nMult:=10
    local nSign:=if(nNumber<0,-1,1) as numeric // Armazena o sinal
    local nDigit as numeric// Armazena o digito extraido
    local nReversed:= 0 as numeric // Armazena o numero revertido
    local nReversedSign as numeric

    hb_Default(@nMin,-2147483648)
    hb_Default(@nMax,2147483647)

    while (nAbs>0)

        nDigit:=(nAbs%nMult) // Extrai o ultimo digito
        nAbs:=Int(nAbs/nMult) // Remove o ultimo digito

        // Verifica possivel estouro de 32 bits
        nReversedSign:=nReversed*nSign
        if (((nReversedSign<nMin).or.(nReversedSign>nMax)))
            exit
        endif

        nReversed*=nMult
        nReversed+=nDigit // Adiciona o digito extraido

    end while

    // Aplica o sinal e valida intervalo
    nReversed*=nSign
    if ((nReversed<nMin).or.(nReversed>nMax))
        nReversed:=0
    endif

    return(nReversed)

static function __ConOut(vList as variadic) as character

    local cOut:="",cValType as character

    local nIdx as numeric

    local uValue as variant

    begin sequence

        if (vList==nil)
            break
        endif

        for nIdx:=1 to vList:vCount
            uValue:=vList:vArgs[nIdx]
            cValType:=ValType(uValue)
            if (cValType=="N")
                cOut+=hb_NToC(uValue)
            else
                cOut+=cValToChar(uValue)
            endif
        next nIdx

    end sequence

    return(cOut)
